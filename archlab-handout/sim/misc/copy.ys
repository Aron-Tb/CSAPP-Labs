# copy_block - Copy src to dest and return xor checksum of src
# author: Aron
# Date: 4/5/2023

# Execution begins at address 0
    .pos 0
    irmovq stack, %rsp
    call main
    halt

.align 8
# Source block
src:
    .quad 0x00a
    .quad 0x0b0
    .quad 0xc00
# Destination block
dest:
    .quad 0x111
    .quad 0x222
    .quad 0x333

# main: 
# long *src in %rdi, long *src in %rsi, long len in %rdx
main:
    irmovq src, %rdi
    irmovq dest, %rsi
    irmovq $3, %rdx
    call copy_block
    ret

copy_block:
    irmovq $8, %r8  # constant 8
    irmovq $1, %r9  # constant 1
    xorq %rax, %rax  # set %rax = 0
    andq %rdx, %rdx  # set CC
    jmp test
    loop:
        mrmovq (%rdi), %rcx  # val = *src, val in %rcx
        addq %r8, %rdi  # src++
        rmmovq %rcx, (%rsi)  # *dest = val
        addq %r8, %rsi  # dest++
        xorq %rcx, %rax 
        subq %r9, %rdx 
    test:
        jne loop
        ret
    .pos 0x200
stack:
